import java_cup.runtime.*;
import java.util.*;
import java.io.*;
import core.symbol.SymbolTable;
import core.symbol.SymbolType;
import core.symbol.Symbol;

parser code {:


    private SymbolTable symbolTable = new SymbolTable();

    public SymbolTable getSymbolTable() {
        return symbolTable;
    }

    public void report_error(String message, Object info) {
        StringBuilder m = new StringBuilder("Error");
        if (info instanceof java_cup.runtime.Symbol) {
            java_cup.runtime.Symbol s = ((java_cup.runtime.Symbol) info);
            if (s.left >= 0) {                
                m.append(" in line "+(s.left+1));   
                if (s.right >= 0)                    
                    m.append(", column "+(s.right+1));
            }
        }
        m.append(" : "+message);
        System.err.println(m);
    }
   
    public void report_fatal_error(String message, Object info) {
        report_error(message, info);
        System.exit(1);
    }
:};

/** Terminals */

terminal           AND, OR, NOT, TRUE, FALSE, SUPTRUE, SUPFALSE; // boolean

// flow
terminal           BEGIN, END, EXIT; 
terminal           IF, THEN, ELSE; 
terminal           WHILE, DO; 

terminal           PRINT, READINT, LENGTH, POSITION, READSTR, CONCATENATE, SUBSTRING, FILEOUT;  // commands
terminal           ASSIGN; 
terminal   	       EQ, LT, LE, GT, GE, NE; // num_rel
terminal    	   STREQ, STRNOTEQ; //str_rel

terminal           SEMI, COMMA, PLUS, MINUS, TIMES, DIVIDE, MODE, LPAREN, RPAREN, INCREMENT, DECREMENT;
terminal Integer   NUM;
terminal String    IDENT;
terminal String    STRING;

//Vars
terminal           BOOLEAN;
terminal           INTEGER;
terminal           CONST;
terminal           TUP;



/** Non-terminals **/
non terminal Expr expr;
non terminal Expr num_expr;
non terminal Expr str_expr;
non terminal Expr bool_expr;
non terminal Expr bool_expr2;
non terminal Expr tuple_expr;
non terminal Expr const_expr;


non terminal Operator num_op;
non terminal Condition num_rel;
non terminal Condition str_rel;

non terminal SimpleInstruction simple_instr;
non terminal InstructionList instr;

non terminal SimpleInstruction assign_stat;
non terminal SimpleInstruction if_stat;
non terminal SimpleInstruction while_stat;
non terminal SimpleInstruction output_stat;
non terminal SimpleInstruction fileoutput_stat;
non terminal Main program;

/** Precedence */
precedence left PLUS, MINUS;
precedence left TIMES, DIVIDE, MODE; 

precedence left OR, AND;
precedence left NOT;

precedence left ELSE;

start with program;

/** ============= GRAMMAR ============= */

/** done */
num_op ::= PLUS             {: RESULT = new PlusOperator(); :}
         | MINUS            {: RESULT = new MinusOperator(); :}
         | TIMES            {: RESULT = new TimesOperator(); :}
         | DIVIDE           {: RESULT = new DivideOperator(); :}
         | MODE             {: RESULT = new ModeOperator(); :}
         ;

expr ::= IDENT:i            {: RESULT = new ID(i); :}
            |num_expr:e     {: RESULT = e; :}
            |str_expr:e     {: RESULT = e; :}
            |bool_expr2:e    {: RESULT = e; :} 
            |tuple_expr:e          {: RESULT = e; :} 
            ;

tuple_expr ::= LPAREN expr:e COMMA expr:e2 RPAREN
                {:
                    RESULT = new TupleExpression(e, e2);
                :}
            ;
const_expr ::= NUM:e
                {:
                    RESULT = new ConstIntExpression(e.intValue());
                :}
            | STRING:s
                {:
                    RESULT = new ConstStringExpression(s.substring(1, s.length()-1));
                :}
            | TRUE
                {:
                    RESULT = new ConstBooleanExpression(true);
                :}
            | FALSE
                {:
                    RESULT = new ConstBooleanExpression(false);
                :}
            ;

bool_expr2 ::= TRUE          {: RESULT = new BoolExpression2(true); :}
            | FALSE         {: RESULT = new BoolExpression2(false); :}
            ;
num_expr ::=  NUM:e
                {:
                    RESULT = new IntExpression(e.intValue());
                :}
            | READINT
                 {:
                    RESULT = new IntEnterExpression();
                 :}
            | MINUS expr:e
                {:
                    RESULT = new UMinusExpression(e);
                :}
            | LPAREN expr:e RPAREN
                {:
                    RESULT = new PIntExpression(e);
                :}
            | LENGTH LPAREN expr:e RPAREN
                {:
                    RESULT = new STRLengthExpression(e);
                :}
            | POSITION LPAREN expr:s COMMA expr:s2 RPAREN
                {:
                    RESULT = new STRPositionExpression(s, s2);
                :}
            | expr:e num_op:o expr:e2
                {:
                    RESULT = new OperatorExpression(e, o, e2);
                :}               
            | INCREMENT expr:i      {: RESULT = new PreIncrementExpression(i); :}
            | DECREMENT expr:i      {: RESULT = new PreDecrementExpression(i); :}
            ;

str_expr ::=  STRING:s
                {:
                    RESULT = new StringExpression(s.substring(1, s.length()-1));
                :}
             | READSTR
                {:
                    RESULT = new StrEnterExpression();
                :}
            | CONCATENATE LPAREN expr:s COMMA expr:s2 RPAREN
                {:
                    RESULT = new ConcatStringExpression(s, s2);
                :}
              | SUBSTRING LPAREN expr:s COMMA expr:pos COMMA expr:length RPAREN
                {:
                    RESULT = new SubStringExpression(s, pos, length);
                :}
             ;

num_rel ::=   EQ         {: RESULT = new EqCond(); :}
            | LT         {: RESULT = new LtCond(); :}
            | LE         {: RESULT = new LeCond(); :}
            | GT         {: RESULT = new GtCond(); :}
            | GE         {: RESULT = new GeCond(); :}
            | NE         {: RESULT = new NeCond(); :}
            ;

str_rel ::= STREQ     {: RESULT = new StrEqCond(); :}
          | STRNOTEQ  {: RESULT = new StrNotEqCond(); :}
          ; 

bool_expr ::= SUPTRUE                               {: RESULT = new BooleanExpression(true); :}
            | SUPFALSE                              {: RESULT = new BooleanExpression(false); :}
            | LPAREN bool_expr:e RPAREN          {: RESULT = new PBooleanExpression(e); :}
            | NOT bool_expr:e                    {: RESULT = new NegationBooleanExpression(e); :}
            | bool_expr:e AND bool_expr:e2       {: RESULT = new AndBooleanExpression(e, e2); :}
            | bool_expr:e OR bool_expr:e2        {: RESULT = new OrBooleanExpression(e, e2); :}
            | expr: e num_rel:o expr:e2          {: RESULT = new ConditionBooleanExpression(e, o, e2); :}
            | expr:s str_rel:o expr:s2           {: RESULT = new ConditionBooleanExpression(s, o, s2); :}
            ;

simple_instr ::= assign_stat:si                  {: RESULT = si; :}
              | if_stat:si                       {: RESULT = si; :}
              | while_stat:si                    {: RESULT = si; :}
              | BEGIN instr:si END               {: RESULT = new BeginEndInstruction(si); :}
              | BEGIN IDENT:i instr:si END       {: RESULT = new BeginEndInstructionId(si, i); :}
              | output_stat:si                   {: RESULT = si; :}
              | fileoutput_stat:si               {: RESULT = si; :}
              | EXIT                             {: System.exit(0); :}
              ;

instr ::= instr:i SEMI simple_instr:si           {: i.add(si); RESULT = i; :}
        | simple_instr:si                        {: RESULT = new InstructionList(si); :}
        ;
assign_stat ::= INTEGER IDENT:i ASSIGN num_expr:e          {: 
        // Aquí obtenemos el tipo del símbolo basado en la expresión
        SymbolType type;
        if (e instanceof IntExpression) {
            type = SymbolType.INTEGER;
        } else {
            type = SymbolType.OTHER;
        }       
        // Añadir el símbolo a la tabla
        symbolTable.put(i.toString(), e, type, false);

        RESULT = new AssignInstruction(i, e); 
    :}
    | BOOLEAN IDENT:i ASSIGN bool_expr2:e           {: 
        // En este caso, aseguramos que sea una expresión booleana válida
        SymbolType type;
        if (e instanceof BoolExpression2 || e instanceof BooleanExpression) {
            type = SymbolType.BOOLEAN;
        } else {
            report_error("Invalid boolean assignment", i);
            type = SymbolType.OTHER;
        }
        // Añadir el símbolo a la tabla
        symbolTable.put(i.toString(), e, type, false);

        RESULT = new AssignInstruction(i, e); 

    :}
    | TUP IDENT:i ASSIGN tuple_expr:e         {:

        SymbolType type;
        if (e instanceof TupleExpression) {
            type = SymbolType.TUPLE;
        } else {
            report_error("Invalid tuple assignment", i);
            type = SymbolType.OTHER;
        }
        // Añadir el símbolo a la tabla
        symbolTable.put(i.toString(), e, type, false);
        
        RESULT = new AssignInstruction(i, e);
    :}
    | CONST IDENT:i ASSIGN const_expr:e          {:
        // Comprobar si el identificador existe en la tabla de símbolos
        Symbol symbol = symbolTable.get(i.toString());
        if (symbol != null && symbol.isConstant()) {
            // Si ya existe, lanzar un error indicando que la constante ya fue definida
            report_error("Constant " + i + " is already defined.", i);
        return null;
        } else {
            // Si no existe, agregarlo a la tabla de símbolos como constante
            SymbolType type;
            if (e instanceof ConstIntExpression) {
                type = SymbolType.CONSTANT;
            } else if (e instanceof ConstStringExpression) {
                type = SymbolType.CONSTANT;
            } else if (e instanceof ConstBooleanExpression) {
                type = SymbolType.CONSTANT;
            } else {
                type = SymbolType.OTHER;
            }

            symbolTable.put(i.toString(), e, type, true);
        }
        RESULT = new AssignInstruction(i, e);

    :}
    | IDENT:i ASSIGN expr:e                   {:

    // ESTE VENDRÍA A SER UNA ASIGNACIÓN QUE NO TIENE VARIABLE
    Symbol symbol = symbolTable.get(i.toString());
    if (symbol != null && symbol.isConstant()) {
        // Si ya existe, lanzar un error indicando que la constante ya fue definida
        report_error("Constant " + i + " is already defined.", i);
        return null;
    } else {
        // Si no existe, agregarlo a la tabla de símbolos como constante
        SymbolType type;
        if (e instanceof IntExpression) {
            type = SymbolType.INTEGER;
        } else if (e instanceof StringExpression) {
            type = SymbolType.STRING;
        } else if (e instanceof BooleanExpression) {
            type = SymbolType.BOOLEAN;
        } else {
            type = SymbolType.OTHER;
        }

        symbolTable.put(i.toString(), e, type, false);
    }
        // Comprobar si el identificador existe en la tabla de símbolos
        RESULT = new AssignInstruction(i, e);
    :}
                ;

if_stat ::= IF bool_expr:c THEN simple_instr:s                          {: RESULT = new IfInstruction(c, s); :}
            | IF bool_expr2:c THEN simple_instr:s                        {: RESULT = new IfInstruction(c, s); :}
            | IF bool_expr:c THEN simple_instr:s ELSE simple_instr:s2   {:  RESULT = new IfElseInstruction(c, s, s2); :}
            | IF bool_expr2:c THEN simple_instr:s ELSE simple_instr:s2   {:  RESULT = new IfElseInstruction(c, s, s2); :}
            ;

while_stat ::= WHILE bool_expr:c DO simple_instr:s       {: RESULT = new WhileInstruction(c, s); :}
            | DO simple_instr:s WHILE bool_expr:c        {: RESULT = new DoWhileInstruction(c, s); :}
            ;

output_stat ::= PRINT LPAREN expr:e RPAREN               {: RESULT = new OutputInstruction(e); :}
            ;

fileoutput_stat ::= FILEOUT LPAREN expr:s COMMA expr:s2 RPAREN    {: RESULT = new FileOutputInstruction(s, s2); :}
            ;

program ::= instr:i
          {:
            RESULT = new Main(i);  // Devuelve una instancia de Main
          :}
          ;